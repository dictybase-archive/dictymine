project(':dictymine:dbmodel') {
    ext.osName = 'os.production'
    ext.model = 'genomic'

    sourceSets {
        main {
            java {
                srcDirs = ["${buildDir}/gen/src"]
            }
            resources {
                srcDirs = ['resources']
            }
        }
    }

    //configurations {
        //objectstore
    //}

    dependencies {
        api project(':bio:core')
        api project(':intermine:model')
        api project(':imbuild:im-ant-tasks')
        api project(':intermine:integrate:model:fulldata')
    }

    task copyModelFile(type: Copy, dependsOn: createBuildFolder) {
        description 'Copies genomic xml model files to build/model directory'
        from file("${project(':bio:core').projectDir}")
        into "${buildDir}/model"
        include "core.xml"
        rename "core.xml", "genomic_model.xml"
    }

    task copyModelFileForCompile(type: Copy) {
        description 'Copies genomic xml model files to build/main directory'
        from "${buildDir}/model/genomic_model.xml"
        into "${buildDir}/main"
    }

    task copyGenomicProps(type: Copy, dependsOn: createBuildFolder) {
        from "${projectDir}/resources"
        into "${buildDir}/model"
        include 'genomic_*'
    }

    task copyAggregate() { 
        dependsOn copyDefaultProperty, copyMineProperty, copyModelFile, copyModelFileForCompile, copyGenomicProps
    }
    copyModelFileForCompile.mustRunAfter(copyModelFile)

    task createSOModel(dependsOn: [copyAggregate, project(':bio:core').tasks.jar]){
        description 'create so model xml definition file'
        doLast {
            ant.taskdef(
                name: 'somodel',
                classname: 'org.intermine.bio.task.SOToModelTask'
            ){
                classpath {
                    pathelement(path: project(':bio:core').sourceSets.main.output.classesDir)
                    pathelement(path: project(':bio:core').sourceSets.main.output.resourcesDir)
                    pathelement(path: project(':intermine:model').sourceSets.main.output.classesDir) 	
                    pathelement(path: configurations.log4j.asPath)
                    pathelement(path: configurations.lang.asPath)
                    pathelement(path: configurations.bbop.asPath)
                    pathelement(path: configurations.obo.asPath)
                    pathelement(path: configurations.collections.asPath)
                }
            }
            ant.somodel(
                soTermListFile: "${rootDir}/bio/test-all/dbmodel/resources/so_term_list.txt",
                soFile: "${rootDir}/bio/sources/so/data/so.obo2.5",
                outputFile: "${buildDir}/model/so_additions.xml" 
            )
        }
    }

    task mergeSourceModel(dependsOn: [
                            createSOModel, 
                            project(':imbuild:im-ant-tasks').tasks.jar,
                        ]) {
        description 'merges all model defintions in xml format'
        doLast {
            ant.taskdef(
                name: 'mergesourcemodels',
                classname: 'org.intermine.task.MergeSourceModelsTask'
            ){
                classpath(id: 'task.class.path' ) {
                    pathelement(path: project(':bio:core').sourceSets.main.output.classesDir)
                    pathelement(path: project(':intermine:objectstore').sourceSets.main.output.classesDir)
                    pathelement(path: project(':intermine:model').sourceSets.main.output.classesDir) 	
                    pathelement(path: project(':imbuild:im-ant-tasks').sourceSets.main.output.classesDir) 	
                    pathelement(path: configurations.log4j.asPath)
                    pathelement(path: configurations.lang.asPath)
                }
            }
            ant.mergesourcemodels(
                classpathref: 'task.class.path',
                projectXml: "${mineDir}/project.xml",
                basedir: rootDir,
                modelFile: "${buildDir}/model/genomic_model.xml",
                extraModelPathsStart: "${mineName}/dbmodel/build/model/so_additions.xml" + ' ' + "bio/core/genomic_additions.xml",
                extraModelPathsEnd: ' '
            )
        }
    }

    task generateModel(dependsOn: mergeSourceModel) {
        description 'Generate genomic model classes from xml definition file'
        doLast {
            ant.taskdef(
                name: 'genomicModelOutput',
                classname: 'org.intermine.task.ModelOutputTask'
            ){
                classpath {
                    pathelement(path: project(':bio:core').sourceSets.main.output.classesDir)
                    pathelement(path: project(':intermine:objectstore').sourceSets.main.output.classesDir)
                    pathelement(path: project(':intermine:model').sourceSets.main.output.classesDir) 	
                    pathelement(path: configurations.log4j.asPath)
                    pathelement(path: configurations.lang.asPath)
                    pathelement(path: "${buildDir}/main")
                }
            }
            ant.genomicModelOutput(
                type: 'java',
                model: 'genomic',
                destdir: "${buildDir}/gen/src"
            )
        }
    }
    assemble.dependsOn(generateModel)

    task buildDb() {
        description 'prepare(builds) database for running data integrations'
        dependsOn assemble, insertmodel, createIndexes, analyseDb
    }
    insertModel.mustRunAfter(assemble)
    createIndexes.mustRunAfter(insertModel)
    analyseDb.mustRunAfter(createIndexes)
}
